# ignore opening tags, white space and comments
IGNORE: "<\?php|([ \f\t\n]*)|(//[^\n]*\n)|(/\*([^\*]|\*[^/])*\*?\*/)";

VARIABLENAME: "\$[a-zA-Z_][a-zA-Z0-9_]*";
IDENTIFIERNAME: "[a-zA-Z_][a-zA-Z0-9_]*";

main: sourceelements [EOF];

sourceelements  : sourceelement >sourceelements<
                | sourceelement
                ;

sourceelement   : <functiondeclaration>
                | <statement>
                ;

functiondeclaration : ["function"] identifier "(" formalparameterlist? ")" ["{"] functionbody? "}"
                    ;

functionexpression  : ["function"] identifier "(" formalparameterlist? ")"
                    ;

formalparameterlist : variable [","] >formalparameterlist<
                    | variable
                    ;

globallist : variable [","] >globallist<
           | variable
           ;

functionbody    : <sourceelements>
                ;

statement : <block>
          | <expressionstatement> [";"]
          | <ifstatement>
          | <iterationstatement>
          | <printstatement> [";"]
          | <globalstatement> [";"]
          | <returnstatement> [";"]
          ;

block   : "{" >statementlist<? ["}"]
        ;

statementlist   : statement >statementlist<
                | statement
                ;

returnstatement : ["return"] expression?
                ;

expressionstatement : expression
                    ;

initialiser : ["="] assignmentexpression
            ;

ifstatement : ["if"] ["("] expression [")"] statement  ["else"] statement
            | ["if"] ["("] expression [")"] statement
            ;

iterationstatement
    : whiles
    | regularfor
    ;

whiles
    : "while" ["("] expression [")"] statement
    ;

regularfor
    : "for" ["("] expression? ";" expression? ";" expression? ")" statement;

printstatement: "print" expression ;

globalstatement: "global" globallist ;

primaryexpression   : <identifier>
                    | <variable>
                    | <literal>
                    | <arrayliteral>
                    | "(" <expression> ")"
                    ;

arrayliteral    : "[" >elementlist<? ["]"]
                | "array(" >elementlist<? [")"]
                | "[" >elementlist<? ["," "]"]
                ;

elementlist : assignmentexpression ([","] assignmentexpression)*
            ;

memberexpression    : primaryexpression >memberexpressiona<+
                    | functionexpression >memberexpressiona<+
                    | <primaryexpression>
                    | <functionexpression>
                    ;

memberexpressiona   : "[" expression ["]"]
                    ;

newexpression   : <memberexpression>
                ;

callexpression  : memberexpression arguments >callexpressiona<*
                ;

callexpressiona : arguments
                ;

arguments   : "(" [")"]
            | "(" >argumentlist< [")"]
            ;

argumentlist    : assignmentexpression [","] >argumentlist<
                | assignmentexpression;

lefthandsideexpression  : <callexpression>
                        | <newexpression>
                        ;

postfixexpression   : lefthandsideexpression "++"
                    | lefthandsideexpression "--"
                    | <lefthandsideexpression>
                    ;

unaryexpression : "++" unaryexpression
                | "--" unaryexpression
                | "!" unaryexpression
                | <postfixexpression>
                ;
joinop: ".";
stringjoinexpression  : unaryexpression (>joinop< unaryexpression)+
                      | <unaryexpression>
                      ;

multop: "*" | "/" | "%";
multiplicativeexpression    : stringjoinexpression (>multop< stringjoinexpression)+
                            | <stringjoinexpression>
                            ;
addop: "+" | "-";
additiveexpression  : multiplicativeexpression (>addop< multiplicativeexpression)+
                    | <multiplicativeexpression>
                    ;

relationalop: "<" | ">" | "<=" | ">=" | "instanceof";
relationalexpression    : additiveexpression (>relationalop< relationalexpression)+
                        | <additiveexpression>
                        ;

equalityop: "==" | "!=" | "===" | "!==";
equalityexpression  : relationalexpression (>equalityop< relationalexpression)+
                    | <relationalexpression>
                    ;

assignmentexpression    : lefthandsideexpression >assignmentoperator< assignmentexpression
                        | <equalityexpression>
                        ;

assignmentoperator  : "=" | "\*=" | "\/=" | "\%=" | "\+=" | "\-=" | "<<="
                    | ">>=" | ">>>=" | "&=" | "^=" | "\|="
                    ;

expression  : assignmentexpression ("," assignmentexpression)+
            | <assignmentexpression>
            ;

identifier: <IDENTIFIERNAME>;
variable: <VARIABLENAME>;

literal : <nullliteral>
        | <booleanliteral>
        | <numericliteral>
        | <stringliteral>
        ;

nullliteral : "null";

booleanliteral  : "true"
                | "false"
                ;

numericliteral  : <DECIMALLITERAL>
                ;

DECIMALLITERAL  : "(((0|[1-9][0-9]*)(\.[0-9]*)?)|(\.[0-9]+))([eE][\+\-]?[0-9]*)?"
                ;

stringliteral   : <SINGLESTRING>
                | <DOUBLESTRING>
                ;

SINGLESTRING    : "'([^'\\]|\\.)*'"
                ;

DOUBLESTRING    : "\"([^\"\\]|\\.)*\""
                ;
