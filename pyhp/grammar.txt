# ignore opening tags, white space and comments
IGNORE: "<\?php|([ \f\t\n]*)|(//[^\n]*\n)|(/\*([^\*]|\*[^/])*\*?\*/)";

VARIABLENAME: "\$[a-zA-Z_][a-zA-Z0-9_]*";
IDENTIFIERNAME: "[a-zA-Z_][a-zA-Z0-9_]*";

main: sourceelements [EOF];

sourceelements  : sourceelement >sourceelements<
                | sourceelement
                ;

sourceelement   : <functiondeclaration>
                | <statement>
                ;

functiondeclaration : ["function"] identifier "(" formalparameterlist? ")" ["{"] functionbody? "}"
                    ;

formalparameterlist : formalparameterlistparam [","] >formalparameterlist<
                    | formalparameterlistparam
                    ;

formalparameterlistparam : variable
                         | "&" variable
                         ;

globallist : variable [","] >globallist<
           | variable
           ;

functionbody    : <sourceelements>
                ;

statement : <block>
          | <constantstatement> [";"]
          | <expressionstatement> [";"]
          | <ifstatement>
          | <iterationstatement>
          | <printstatement> [";"]
          | <globalstatement> [";"]
          | <returnstatement> [";"]
          ;

block   : "{" >statementlist<? ["}"]
        ;

statementlist   : statement >statementlist<
                | statement
                ;

returnstatement : ["return"] expression?
                ;

expressionstatement : expression
                    ;

initialiser : ["="] assignmentexpression
            ;

ifstatement : ["if"] ["("] expression [")"] statement  ["else"] statement
            | ["if"] ["("] expression [")"] statement
            ;

iterationstatement
    : whiles
    | regularfor
    ;

whiles
    : "while" ["("] expression [")"] statement
    ;

regularfor
    : "for" ["("] expression? ";" expression? ";" expression? ")" statement;

printstatement: "print" expression
              | "echo" expression
              ;

globalstatement: "global" globallist ;

constantstatement: "define" ["("] literal "," expression [")"] ;

primaryexpression   : <variable>
                    | <literal>
                    | <arrayliteral>
                    | "(" <expression> ")"
                    ;

arrayliteral    : "[" >elementlist<? ["]"]
                | "array(" >elementlist<? [")"]
                | "[" >elementlist<? ["," "]"]
                ;

elementlist : assignmentexpression ([","] assignmentexpression)*
            ;

memberexpression    : primaryexpression >memberexpressiona<+
                    | <primaryexpression>
                    ;

memberexpressiona   : "[" expression ["]"]
                    ;

newexpression   : <memberexpression>
                ;

callexpression  : identifier arguments >callexpressiona<*
                ;

callexpressiona : arguments
                ;

arguments   : "(" [")"]
            | "(" >argumentlist< [")"]
            ;

argumentlist    : assignmentexpression [","] >argumentlist<
                | assignmentexpression;

lefthandsideexpression  : <callexpression>
                        | <newexpression>
                        ;

postfixexpression   : lefthandsideexpression "++"
                    | lefthandsideexpression "--"
                    | <lefthandsideexpression>
                    ;

unaryexpression : "++" unaryexpression
                | "--" unaryexpression
                | "!" unaryexpression
                | <postfixexpression>
                | <constantexpression>
                ;
joinop: ".";
stringjoinexpression  : unaryexpression (>joinop< unaryexpression)+
                      | <unaryexpression>
                      ;

multop: "*" | "/" | "%";
multiplicativeexpression    : stringjoinexpression (>multop< stringjoinexpression)+
                            | <stringjoinexpression>
                            ;
addop: "+" | "-";
additiveexpression  : multiplicativeexpression (>addop< multiplicativeexpression)+
                    | <multiplicativeexpression>
                    ;

relationalop: "<" | ">" | "<=" | ">=" | "instanceof";
relationalexpression    : additiveexpression (>relationalop< relationalexpression)+
                        | <additiveexpression>
                        ;

equalityop: "==" | "!=" | "===" | "!==";
equalityexpression  : relationalexpression (>equalityop< relationalexpression)+
                    | <relationalexpression>
                    ;

logicalandexpression    : equalityexpression ("&&" equalityexpression)+
                        | <equalityexpression>
                        ;

logicalorexpression : logicalandexpression ("||" logicalandexpression)+
                    | <logicalandexpression>
                    ;

conditionalexpression   : logicalorexpression "?" assignmentexpression [":"] assignmentexpression
                        | <logicalorexpression>
                        ;

assignmentexpression    : lefthandsideexpression >assignmentoperator< assignmentexpression
                        | <conditionalexpression>
                        ;

assignmentoperator  : "=" | "\*=" | "\/=" | "\%=" | "\+=" | "\-=" | "<<="
                    | ">>=" | ">>>=" | "&=" | "^=" | "\|=" | ".="
                    ;

expression  : assignmentexpression ("," assignmentexpression)+
            | <assignmentexpression>
            ;

constantexpression: constant;

identifier: <IDENTIFIERNAME>;
variable: <VARIABLENAME>;
constant: <IDENTIFIERNAME>;

literal : <nullliteral>
        | <booleanliteral>
        | <numericliteral>
        | <stringliteral>
        ;

nullliteral : "null";

booleanliteral  : "true"
                | "false"
                ;

numericliteral  : DECIMALLITERAL
                | "-" DECIMALLITERAL
                | "+" DECIMALLITERAL
                ;

DECIMALLITERAL  : "(((0|[1-9][0-9]*)(\.[0-9]*)?)|(\.[0-9]+))([eE][\+\-]?[0-9]*)?"
                ;

stringliteral   : <SINGLESTRING>
                | <DOUBLESTRING>
                ;

SINGLESTRING    : "'([^'\\]|\\.)*'"
                ;

DOUBLESTRING    : "\"([^\"\\]|\\.)*\""
                ;
